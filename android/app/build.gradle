def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}
def hasLocalKeyProperties = localProperties.containsKey('storeFile')

def flutterRoot = localProperties.getProperty('flutter.sdk')
if (flutterRoot == null) {
    throw new GradleException("Flutter SDK not found. Define location with flutter.sdk in the local.properties file.")
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = '1'
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply from: "$flutterRoot/packages/flutter_tools/gradle/flutter.gradle"

String getCredential(String envKey, String localProperty) {
    final String envValue = System.getenv(envKey)
    if (envValue == null || envValue.isEmpty()) {
        return localProperty
    }
    return envValue
}

android {
    compileSdkVersion 28

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        disable 'InvalidPackage'
    }

    defaultConfig {
        // TODO: Specify your own unique Application ID (https://developer.android.com/studio/build/application-id.html).
        applicationId "games.yuki.salmon_stats_android"
        minSdkVersion 16
        targetSdkVersion 28
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        release {
            if (hasLocalKeyProperties) {
                keyAlias getCredential('KEY_ALIAS', localProperties['keyAlias'])
                keyPassword getCredential('KEY_PASSWORD', localProperties['keyPassword'])
                storeFile file(getCredential('KEY_STORE_FILE', localProperties['storeFile']))
                storePassword getCredential('KEY_STORE_PASSWORD', localProperties['storePassword'])
            }
        }
    }

    buildTypes {
        release {
            if (hasLocalKeyProperties) {
                println "Signing with keystore"

                signingConfig signingConfigs.release
            } else {
                println "Signing with debug key"

                signingConfig signingConfigs.debug
            }
        }
    }
}

flutter {
    source '../..'
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
}
